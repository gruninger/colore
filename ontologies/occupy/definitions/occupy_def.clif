/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved.
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
 *******************************************************************************/

(cl-text  http://colore.oor.net/occupy/definitions/occupy_def.clif

(cl-imports http://colore.oor.net/occupy/occupy_root.clif)

(cl-domain region (cl-imports  http://colore.oor.net/mereology/definitions/ppart.clif))

(cl-domain region (cl-imports http://colore.oor.net/combined_mereotopology/definitions/mt_def.clif))

(forall (x y)
(iff    (physical_overlaps x y)
        (exists (z)
                (and    (physical_part z x)
                        (physical_part z y)))))

(forall (x y)
(iff    (physical_ppart x y)
	(and    (physical_part x y)
		(not (= x y)))))

(forall (x)
(iff    (physical_atom x)
	(and    (physical_object x)
		(not (exists (y)
			(and	(physical_object y)
				(physical_ppart y x)))))))

(forall (x)
(iff    (atomic_region x)
	(and    (region x)
		(not (exists (y)
			(and	(region y)
				(ppart y x)))))))

(forall (x)
(iff	(physical_gunk x)
	(and	(physical_object x)
		(forall (y)
			(if	(and	(physical_object y)
					(physical_part y x))
				(exists (z)
					(and	(physical_object z)
						(physical_ppart z y))))))))

(forall (x)
(iff	(gunky x)
	(and	(region x)
		(forall (y)
			(if	(and	(region y)
					(part y x))
				(exists (z)
					(and	(region z)
						(ppart z y))))))))

(forall (o r)
	(iff	(POcc o r)
		(exists (z)
			(and	(part z r)
				(occupies o z)))))

(forall (o r)
	(iff	(WOcc o r)
		(exists (z)
			(and	(part r z)
				(occupies o z)))))

(forall (o r)
	(iff	(IPOcc o r)
		(exists (z)
			(and	(IP z r)
				(occupies o z)))))

(forall (o r)
	(iff	(TPOcc o r)
		(exists (z)
			(and	(TP z r)
				(occupies o z)))))

(forall (o r)
	(iff	(IWOcc o r)
		(exists (z)
			(and	(IP r z)
				(occupies o z)))))

(forall (o r)
	(iff	(TWOcc o r)
		(exists (z)
			(and	(TP r z)
				(occupies o z)))))

)
