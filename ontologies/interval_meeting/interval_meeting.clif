/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved.
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
 *******************************************************************************/
 

(cl-text http://colore.oor.net/interval_meeting/interval_meeting.clif

(forall (i j)
(if	(meets i j)
	(and	(timeinterval i)
		(timeinterval j))))

(forall (i j k m)
(if	(and	(meets i k)
		(meets j k)
		(meets i m))
	(meets j m)))

(forall (i)
	(exists (j k)
		(and	(meets j i)
			(meets i k))))

(forall (i j k l)
(if	(and	(meets i j)
		(meets k l))
	(or	(meets i l)
		(exists (n)
			(or	(and	(meets i n)
					(meets n l))
				(and	(meets k n)
					(meets n j)))))))

(forall (i j)
(if	(meets i j)
	(not (meets j i))))

(forall (i j k m)
(if	(and	(meets i j)
		(meets j k)
		(meets k m))
	(exists (n)
		(and	(meets i n)
			(meets n m)))))

)
