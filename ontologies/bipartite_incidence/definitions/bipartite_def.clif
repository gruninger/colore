/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved.
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
 *******************************************************************************/

(cl-text http://colore.oor.net/bipartite_incidence/definitions/bipartite_def.clif

(cl-imports http://colore.oor.net/bipartite_incidence/weak_bipartite.clif)

(forall (p1 p2)
	(iff	(collinear p1 p2)
		(and	(point p1)
			(point p2)
			(not (= p1 p2))
			(exists (l)
				(and	(line l)
					(in p1 l)
					(in p2 l))))))

(forall (l1 l2)
	(iff	(intersect l1 l2)
		(and	(line l1)
			(line l2)
			(not (= l1 l2))
			(exists (p)
				(and	(point p)
					(in p l1)
					(in p l2))))))

(forall (l1 l2)
	(iff	(parallel l1 l2)
		(and	(line l1)
			(line l2)
			(not (exists (p)
				(and	(point p)
					(in p l1)
					(in p l2)))))))

(forall (l1 l2)
	(iff    (nontrivial_segment l1 l2)
		(and    (exists (p1)
				(and	(point p1)
					(in p1 l1)))
			(forall (p2)
				(if	(and    (point p2)
						(in p2 l1))
					(in p2 l2))))))

(forall (l1 l2)
	(iff	(adj l1 l2)
		(and	(line l1)
			(line l2)
			(exists (p)
				(and	(point p)
					(in p l1)
					(in p l2))))))

(forall (y)
        (iff    (interior_point y)
                (exists (l)
                        (and    (point y)
				(line l)
                                (in y l)
                                (forall (z)
                                        (if     (and    (line z)
                                                        (in y z))
                                                (= z l)))))))

(forall (p)
	(iff	(intersecting_point p)
		(and	(point p)
			(exists (l1 l2)
				(and	(line l1)
					(line l2)
					(not (= l1 l2))
					(in p l1)
					(in p l2))))))

(forall (l1 l2)
	(iff	(overlap l1 l2)
		(and	(intersect l1 l2)
			(exists (p)
				(and	(point p)
					(in p l1)
					(not (in p l2)))))))

(forall (x)
	(iff	(pendant_point x)
		(and	(point x)
			(forall (y z)
				(if	(and	(collinear x y)
						(collinear x z))
					(= y z))))))

)
