/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved.
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
 *******************************************************************************/
 
(cl-text http://colore.oor.net/periods/definitions/periods_def.clif

(cl-imports http://colore.oor.net/periods/periods_root.clif)

(cl-comment 'Overlaps')
(forall	(x y)
(iff	(overlaps x y)
	(exists	(z)
		(and	(inclusion z x)
			(inclusion z y)))))

(cl-comment 'Greatest Lower Bound')
(forall (x y z)
(iff	(glb x y z)
	(and	(inclusion z x)
		(inclusion z y)
		(forall (u)
			(if	(and	(inclusion u x)
					(inclusion u y))
				(inclusion u z))))))

(cl-comment 'Underlaps')
(forall (x y)
(iff    (underlaps x y)
	(exists (z)
		(and    (inclusion x z)
			(inclusion y z)))))

(cl-comment 'Lower Upper Bound')
(forall (x y z)
(iff    (lub x y z)
	(and    (inclusion x z)
		(inclusion y z)
		(forall (u)
			(if     (and    (inclusion x u)
					(inclusion y u))
				(inclusion z u))))))


)
