/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved.
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
*******************************************************************************/


(cl-text http://colore.oor.net/boxworld/boxworld_peak.clif

(cl-imports http://colore.oor.net/boxworld/boxworld_edge.clif)

(forall (e1 e2 e3)
        (if     (rbetween e1 e2 e3) (rbetween e2 e3 e1)))

(forall (e1 e2 e3)
        (if     (rbetween e1 e2 e3)
                (not (rbetween e3 e2 e1))))

(forall (e1 e2 e3 e4)
        (if     (and    (rbetween e1 e2 e3) (rbetween e1 e3 e4))
                (rbetween e1 e2 e4)))

(forall (e1 e2 e3)
        (if     (rbetween e1 e2 e3)
                (exists (v)
                        (and    (vertex v) (edge e1) (edge e2) (edge e3)
                                (part v e1) (part v e2) (part v e3)))))

(forall (v e1 e2 e3)
        (if     (and    (vertex v) (edge e1) (edge e2) (edge e3)
                        (not (= e1 e2)) (not (= e1 e3)) (not (= e2 e3))
                        (part v e1) (part v e2) (part v e3))
                (or     (rbetween e1 e2 e3) (rbetween e2 e3 e1) (rbetween e3 e1 e2))))

(forall (e1 e2 e3 e4)
        (if     (and    (border e1) (border e2) (ridge e3) (ridge e4)
                        (rbetween e1 e3 e2))
                (not (rbetween e2 e4 e1))))
)

