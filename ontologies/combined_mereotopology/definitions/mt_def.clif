/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved.
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
*******************************************************************************/


(cl-text  http://colore.oor.net/combined_mereotopology/definitions/mt_def.clif

(cl-imports  http://colore.oor.net/combined_mereotopology/mt.clif)

(cl-imports  http://colore.oor.net/mereology/definitions/mereology_def.clif)

%Disconnected
(forall (x y)
	(iff	(DC x y)
		(not (C x y))))

%Proper Overlap
(forall (x y)
	(iff	(PO x y)
		(and	(overlaps x y)
			(not (part x y))
			(not (part y x)))))

%External Connection
(forall (x y)
	(iff	(EC x y)
		(and	(C x y)
			(not (overlaps x y)))))

%Tangential Proper Part
(forall (x y)
	(iff	(TPP x y)
		(and	(part x y)
			(not (= x y))
			(exists (z)
				(and	(EC z x)
					(EC z y))))))

%Non-Tangential Proper Part
(forall (x y)
	(iff	(NTPP x y)
		(and	(part x y)
			(not (= x y))
			(not (exists (z)
				(and	(EC z x)
					(EC z y)))))))

%Enclosure
(forall (x y)
	(iff	(E x y)
		(forall (z)
			(if	(C z x)
				(C z y)))))

(forall (x y)
	(iff	(superposition x y)
		(exists (z)
			(and	(E z x)
				(E z y)))))

(forall (x y)
	(iff	(proper_superposition x y)
		(and	(superposition x y)
			(not (E x y))
			(not (E y x)))))

(forall (x y)
	(iff	(boundary_connected x y)
		(and	(C x y)
			(not (superposition x y)))))

(forall (x y)
	(iff	(internal_enclosure x y)
		(forall (z)
			(if	(C z x)
				(S z y)))))

(forall (x y)
	(iff	(proper_internal_enclosure x y)
		(and	(internal_enclosure x y)
			(E x y)
			(not (E y x)))))

(forall (x y)
	(iff	(tangential_enclosure x y)
		(and	(E x y)
			(not (internal_enclosure x y)))))

(forall (x y)
	(iff	(proper_tangential_enclosure x y)
		(and	(tangential_enclosure x y)
			(proper_enclosure y x))))

(forall (x y)
	(iff	(IP x y)
		(and	(part x y)
			(forall (z)
				(if	(C z x)
					(overlaps z y))))))

(forall (x y)
	(iff	(TP x y)
		(and	(part x y)
			(not (IP x y)))))

(forall (x y)
	(iff	(IO x y)
		(exists (z)
			(and	(IP z x)
				(IP z y)))))

(forall (x y)
	(iff	(TO x y)
		(and	(overlaps x y)
			(not (IO x y)))))

(forall (x y)
	(iff	(IU x y)
		(exists (z)
			(and	(IP x z)
				(IP y z)))))

(forall (x y)
	(iff	(TU x y)
		(and	(underlaps x y)
			(not (IU x y)))))

(forall (x y)
	(iff	(IPP x y)
		(and	(IP x y)
			(not (IP y x)))))

(forall (x)
	(iff	(SC x)
		(forall (y z)
			(if	(forall (w)
					(iff	(overlaps w x)
						(or	(overlaps w y)
							(overlaps w z))))
				(C y z)))))


)
