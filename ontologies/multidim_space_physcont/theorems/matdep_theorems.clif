/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 4.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/4.0/legalcode.
 *
 * Contributors:
 *    Torsten Hahmann - initial implementation
 *******************************************************************************/

(cl-text http://colore.oor.net/multidim_space_physcont/theorems/matdep_theorems.clif

(cl-imports http://colore.oor.net/multidim_space_physcont/matdep.clif)

(cl-imports http://colore.oor.net/multidim_space_dim/definitions/min_max_dim.clif)

(cl-comment 'MDep-T1: matdep is symmetric')

(forall (x y)
	(if
		(matdep x y)
		(matdep y x)
	)
)


(cl-comment 'MDep-T2: matdep is reflexive for mat or V')

(forall (x)
	(if
		(mat x)
		(matdep x x)
	)
)

(forall (x)
	(if
		(V x)
		(matdep x x)
	)
)

(cl-comment 'MDep-T3: only entities of equal spatial dimension and of codimension 0 can be matdep')

(forall (x y)
    (if
        (matdep x y)
        (EqDim (r x) (r y))
    )
)

(forall (x y)
    (if
        (matdep x y)
        (MaxDim (r x))
    )
)


(forall (x y)
    (if
        (matdep x y)
        (MaxDim (r y))
    )
)




)