/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 3.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/3.0/legalcode.
 *
 * Contributors:
 *    Torsten Hahmann - initial implementation
 *******************************************************************************/

(cl-text http://colore.oor.net/multidim_space_physcont/dep.clif

(cl-imports http://colore.oor.net/multidim_space_voids/voids_multigran.clif)

(cl-imports http://colore.oor.net/multidim_space_ped/definitions/mat.clif)


(cl-comment 'Dep-A1: dependence is a relation between physical endurants')

(forall (x y)
	(if
		(dep x y)
		(PED x)
	)
)

(forall (x y)
	(if
		(dep x y)
		(PED y)
	)
)


(cl-comment 'Dep-A2: dependence is symmetric')

(forall (x y)
	(if
		(dep x y)
		(dep y x)
	)
)


(cl-comment 'Dep-A3: material endurants are dependent iff they spatially overlap')

(forall (x y)
	(if
		(and
			(mat x)
			(mat y)
		)
		(iff
			(dep x y)
			(PO (r x) (r y))
		)
	)
)


(cl-comment 'Dep-A4: a void and a material endurant are dependent iff they are in a hosts relation')

(forall (x y)
	(if
		(and
			(V x)
			(mat y)
		)
		(iff
			(dep y x)
			(hostsvany y x)
		)
	)
)



(cl-comment 'Dep-A5: voids are dependent iff they overlap and have spatially nested material hosts')

(forall (x y)
	(if
		(and
			(V x)
			(V y)
		)
		(iff
			(dep x y)
			(and
				(PO (r x) (r y))
				(exists (hx hy)
					(and
						(hostsv hx x)
						(hostsv hy y)
						(mat hx)
						(mat hy)
						(or
							(P (r hx) (r hy))
							(P (r hy) (r hx))
						)
					)
				)
			)
		)
	)
)

(cl-comment 'Dep-A6 (from 2014 FOIS paper Interdependence among material objects and voids)')

(forall (x)
    (if
        (PED x)
        (dep x x)
    )
)


)