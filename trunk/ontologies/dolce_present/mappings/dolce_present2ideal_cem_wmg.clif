/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 3.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/3.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger, Carmen Chui
 *******************************************************************************/
 
(cl-text dolce_present2ideal_cem_wmg

(cl-comment "Translation definition for dolce_present into ideal_cem_wmg.")

(forall (x y)
	(iff (in x y)
		(or
			(and (PRE x y)(T y)(or (ED x)(PD x)(Q x)))
			(and (PRE y x)(T x)(or (ED y)(PD y)(Q y)))
			(and (= x y)(or (ED y)(PD y)(Q y)(T y))))))

(forall (x)
	(iff (line x)
		(or (ED x)(PD x)(Q x))))
		
(forall (x)
	(iff (point x)
		(T x)))
		
(forall (x y)
	(iff (part x y)
		(and (P x y)(T x)(T y))))
		
(forall (x)
	(iff (L_1 x)
		(ED x)))
	
(forall (x)
	(iff (L_2 x)
		(PD x)))

(forall (x)
	(iff (L_3 x)
		(Q x)))

(forall (x)
	(iff (L_4 x)
		(PED x)))

(forall (x)
	(iff (L_5 x)
		(NPED x)))

(cl-comment "Additional translation definitions for relations")
(forall (x y)
	(iff (ppart x y)
		(and (PP x y)(T x)(T y))))

(forall (x y)
	(iff (overlaps x y)
		(and (O x y)(T x)(T y))))

(forall (x y)
	(iff (underlaps x y)
		(and (U x y)(T x)(T y))))

(forall (x y)
	(iff (disjoint x y)
		(and (DJ x y)(T x)(T y))))

(forall (x y z)
	(iff (sum x y z)
		(and (SUM z x y)(T z)(T x)(T y))))

(cl-comment "cem_mereology definition for sum")
(forall (x y z)
	(iff	(sum x y z)
		(forall (w)
			(iff	(overlaps w z)
				(or	(overlaps w x)
					(overlaps w y))))))

)