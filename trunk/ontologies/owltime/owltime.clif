
(cl-text http://colore.oor.net/owltime/owltime.clif

(forall (t1 t2)
	(if	(before t1 t2)
		(not (= t1 t2))))

(forall (t1 t2)
	(if	(before t1 t2)
		(not (before t2 t1))))

(forall (t1 t2 t3)
	(if	(and	(before t1 t2)
			(before t2 t3))
		(before t1 t3)))

(forall (t)
	(if	(Instant t)
		(TemporalEntity t)))

(forall (t)
	(if	(Interval t)
		(TemporalEntity t)))

(forall (t)
	(if	(TemporalEntity t)
		(or	(Interval t)
			(Instant t))))

(forall (t x)
	(if	(begins t x)
		(and	(Instant t)
			(TemporalEntity x))))

(forall (t x)
	(if	(ends t x)
		(and	(Instant t)
			(TemporalEntity x))))

(forall (t)
	(iff	(Instant t)
		(begins t t)))

(forall (t)
	(iff	(Instant t)
		(ends t t)))

(forall (x t1 t2)
	(if	(and	(TemporalEntity x)
			(begins t1 x)
			(begins t2 x))
		(= t1 t2)))

(forall (x t1 t2)
	(if	(and	(TemporalEntity x)
			(ends t1 x)
			(ends t2 x))
		(= t1 t2)))

(forall (x t1 t2)
	(if	(timeBetween x t1 t2)
		(and	(TemporalEntity x)
			(Instant t1)
			(Instant t2))))

(forall (t1 t2)
	(if	(not (= t1 t2))
		(forall (x)
			(iff	(timeBetween x t1 t2)
				(and	(begins t1 x)
					(ends t2 x))))))

(forall (x t1 t2)
	(if	(and	(Interval x)
			(begins t1 x)
			(ends t2 x))
		(not (before t2 t1))))

(forall (x t1 t2)
	(if	(and	(ProperInterval x)
			(begins t1 x)
			(ends t2 x))
		(before t1 t2)))

(forall (t1 t2)
	(if	(and	(Instant t1)
			(Instant t2)
			(before t1 t2))
		(exists (x)
			(timeBetween x t1 t2))))

(forall (x)
	(iff	(ProperInterval x)
		(and	(Interval x)
			(forall (t1 t2)
				(if	(and	(begins t1 x)
						(ends t2 x))
					(not (= t1 t2)))))))

(forall (t x)
	(if	(inside t x)
		(and	(Instant t)
			(Interval x))))

(forall (t x)
	(iff	(beginsOrIn t x)
		(or	(begins t x)
			(inside t x))))

(forall (t t1 t2 x)
	(if	(and	(inside t x)
			(begins t1 x)
			(ends t2 x)
			(ProperInterval x))
		(and	(before t1 t)
			(before t t2))))

(forall (t1 t2 i)
	(if	(and	(inside t1 i)
			(inside t2 i))
		(or	(before t1 t2)
			(before t2 t1)
			(= t1 t2))))

(forall (i t1 t2)
	(if	(and	(inside t1 i)
			(begins t2 i))
		(before t2 t1)))

(forall (i t1 t2)
	(if	(and	(inside t1 i)
			(ends t2 i))
		(before t1 t2)))

(forall (e)
	(if	(Eventuality e)
		(not (TemporalEntity e))))

(forall (e t)
	(if	(atTime e t)
		(Eventuality e)))

(forall (e t)
	(if	(during e t)
		(Eventuality e)))

(forall (t e)
	(if	(timeSpan t e)
		(and	(Eventuality e)
			(TemporalEntity t))))

(forall (e t)
	(if	(atTime e t)
		(Instant t)))

(forall (e t)
	(if	(during e t)
		(Interval t)))

(forall (e t1 t2)
	(if	(and	(during e t1)
			(inside t2 t1))
		(atTime e t2)))

(forall (e t t1)
	(if	(and	(during e t)
			(intDuring t1 t))
		(during e t1)))

(forall (t e)
	(if	(and	(timeSpan t e)
			(Interval t))
		(during e t)))

(forall (t e)
	(if	(and	(timeSpan t e)
			(Instant t))
		(atTime e t)))

(forall (t1 t2 e)
	(if	(and	(timeSpan t1 e)
			(Interval t2)
			(atTime e t2))
		(or	(inside t2 t1)
			(begins t2 t1)
			(ends t2 t1))))

(forall (t t1 e)
	(if	(and	(timeSpan t e)
			(Instant t)
			(atTime e t1))
		(= t t1)))

)
