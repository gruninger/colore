/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 3.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/3.0/legalcode.
 *
 * Contributors:
 *    Torsten Hahmann - initial implementation
 *******************************************************************************/

(cl-text rcc

(cl-comment "Axioms of the Region Connection Calculus")

(cl-comment "p: Parthood")

(forall (x y)
	(iff 	(p x y)
		(forall (z) 
				(if 	(c z x)
					(c z y) )))
)


(cl-comment "pp: Proper Parthood")

(forall (x y)
	(iff 	(pp x y)
			(and	(p x y)
					(p y x))))


(cl-comment "o: Overlap")

(forall (x y)
	(iff 	(o x y)
		(exists (z)
				(and 	(p z x)
					(p z y) )))
)


(cl-comment "ec: External connection")

(forall (x y)
	(iff 	(ec x y)
		(and 	(c x y)
			(not (o x y)) ))
)


(cl-comment "ntpp: Non-tangential parthood")

(forall (x y)
	(iff 	(ntpp x y)
		(and 	(pp x y)
			(not (exists (z) 
					(and 	(ec z x)
						(ec z y))
			)) ))
)


(cl-comment "R1: Reflexivity")

(forall (x) 
		(c x x))

(cl-comment "R2: Symmetry")

(forall (x y)
	(if 	(c x y)
		(c y x)))

(cl-comment "R3: Universal element")

(forall (x)
	(c x universe)))	

(cl-comment "R4a")

(forall (x y)
		(and	(not (= x universe))		
					(iff	(c x (compl y))
							(not (ntpp x y))
)	)	)

(cl-comment "R4b")

(forall (x y)
		(and	(not (= x universe))		
					(iff	(o x (compl y))
							(not (p x y))
)	)	)


(cl-comment "R5: Sum")

(forall (x y z) 	
	(iff 	(c x (sum y z)) 
		(or 	(c x y)
				(c x z))
)	) 	


(cl-comment "R6: Product")

(forall (x y z) 	
	(if 	(o y z)
			(iff 	(c x (prod y z)) 
					(exists (w)
						(and 	(p w y)
									(p w z)
									(c x w)
)	) 		)		)	)

(cl-comment "R8: Existence of non-tangential parts")

(forall (x)
	(exists (y)
		(ntpp y x)
	)
)


)
