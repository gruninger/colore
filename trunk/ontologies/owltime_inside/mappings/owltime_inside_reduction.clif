
(cl-text http://colore.oor.net/owltime_inside/mappings/owltime_interval_reduction.clif

(forall (x y)
	(iff	(before x y)
		(and	(leq x y)
			(between x x y))))

(forall (x)
	(iff	(Instant x)
		(point x)))

(forall (x)
	(iff	(Interval x)
		(line x)))

(forall (x)
	(iff	(TemporalEntity x)
		(or	(point x)
			(line x))))

(forall (x y)
	(iff	(begins x y)
		(and	(point x)
			(or (line y) (= y x))
			(in x y)
			(forall (z)
				(if	(and	(point z)
						(in z y))
					(leq x z))))))


(forall (x y)
	(iff	(ends x y)
		(and	(point x)
			(or (line y) (= y x))
			(in x y)
			(forall (z)
				(if	(and	(point z)
						(in z y))
					(leq z x))))))

(forall (x y z)
	(iff	(timeBetween x y z)
		(and	(line x)
			(point y)
			(point z)
			(in y x)
			(in z x)
			(lt y z))))

(forall (x y)
	(iff	(inside x y)
		(and	(point x)
			(line y)
			(in_semilinear_log x y)
			(forall (w z)
				(if	(and	(in w y)
						(in z y)
						(not (= w z)))
					(between w x z))))))

)
