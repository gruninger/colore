
(cl-text http://colore.oor.net/date_time_vocabulary/date_time_vocabulary

(forall ((t1 "time interval") (t2 "time interval"))
	(if 	("time interval1 overlaps time interval2" t1 t2)
		(not ("time interval1 is before time interval2" t1 t2))))

(forall ((t1 "time interval") (t2 "time interval"))
	(if 	(not ("time interval1 overlaps time interval2" t1 t2))
		(or 	("time interval1 is before time interval2" t1 t2)
			("time interval1 is before time interval2" t2 t1))))

(forall ((t1 "time interval"))
	(not ("time interval1 is before time interval2" t1 t1)))

(forall ((t1 "time interval") (t2 "time interval"))
	(if 	("time interval1 is before time interval2" t1 t2)
		(not ("time interval1 is before time interval2" t2 t1))))

(forall ((t1 "time interval") (t2 "time interval"))
	(or 	(and 	("time interval1 overlaps time interval2" t1 t2)
			(not ("time interval1 is before time interval2" t1 t2))
			(not ("time interval1 is before time interval2" t2 t1)))
		(and 	("time interval1 is before time interval2" t1 t2)
			(not ("time interval1 overlaps time interval2" t1 t2))
			(not ("time interval1 is before time interval2" t2 t1)))
		(and 	("time interval1 is before time interval2" t2 t1)
			(not ("time interval1 overlaps time interval2" t1 t2))
			(not ("time interval1 is before time interval2" t1 t2)))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval"))
	(if 	(and 	("time interval1 is before time interval2" t1 t2)
			("time interval1 is before time interval2" t2 t3))
		("time interval1 is before time interval2" t1 t3)))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 is properly before time interval2" t1 t2)
		(and 	("time interval1 is before time interval2" t1 t2)
			(exists ((t3 "time interval"))
				(and 	("time interval1 is before time interval2" t1 t3)
					("time interval1 is before time interval2" t3 t2))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 equals time interval2" t1 t2)
		(and 	("time interval1 is part of time interval2" t1 t2)
			("time interval1 is part of time interval2" t2 t1))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 meets time interval2" t1 t2)
		(and 	("time interval1 is before time interval2" t1 t2)
			(not (exists ((t3 "time interval"))
				(and 	("time interval1 is before time interval2" t1 t3)
					("time interval1 is before time interval2" t3 t2)))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 properly overlaps time interval2" t1 t2)
		(and 	("time interval1 overlaps time interval2" t1 t2)
			(exists ((t3 "time interval"))
				(and 	("time interval1 is proper part of time interval2" t3 t1)
					("time interval1 is before time interval2" t3 t2))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 is properly during time interval2" t1 t2)
		(and 	("time interval1 is proper part of time interval2" t1 t2)
			(exists ((t3 "time interval") (t4 "time interval"))
				(and 	("time interval1 is proper part of time interval2" t3 t2)
					("time interval1 is proper part of time interval2" t4 t2)
					("time interval1 is before time interval2" t3 t1)
					("time interval1 is before time interval2" t1 t4))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 starts time interval2" t1 t2)
		(and 	("time interval1 is proper part of time interval2" t1 t2)
			(not (exists ((t3 "time interval"))
				(and 	("time interval1 is proper part of time interval2" t3 t2)
					("time interval1 is before time interval2" t3 t1)))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 finishes time interval2" t1 t2)
		(and 	("time interval1 is proper part of time interval2" t1 t2)
			(not (exists ((t3 "time interval"))
				(and 	("time interval1 is proper part of time interval2" t3 t2)
					("time interval1 is before time interval2" t1 t3)))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 precedes time interval2" t1 t2)
		(or 	("time interval1 is properly before time interval2" t1 t2)
			("time interval1 meets time interval2" t1 t2))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 begins time interval2" t1 t2)
		(or 	("time interval1 equals time interval2" t1 t2)
			("time interval1 meets time interval2" t1 t2)
			("time interval1 properly overlaps time interval2" t1 t2)
			("time interval1 starts time interval2" t1 t2))))

(forall ((t1 "time interval") (t2 "time interval"))
	(iff 	("time interval1 ends time interval2" t1 t2)
		(or 	("time interval1 equals time interval2" t1 t2)
			("time interval1 meets time interval2" t2 t1)
			("time interval1 properly overlaps time interval2" t1 t2)
			("time interval1 finishes time interval2" t1 t2))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval"))
	(iff 	("time interval1 plus time interval2 is time interval3" t1 t2 t3)
		(or 	(if 	(or 	("time interval1 is before time interval2" t1 t2)
					("time interval1 properly overlaps time interval2" t1 t2))
				(and 	("time interval1 starts time interval2" t1 t3)
					("time interval1 finishes time interval2" t2 t3)))
			(if 	(or 	("time interval1 is before time interval2" t2 t1)
					("time interval1 properly overlaps time interval2" t2 t1))
				(and 	("time interval1 starts time interval2" t2 t3)
					("time interval1 finishes time interval2" t1 t3)))
			(if 	("time interval1 is part of time interval2" t1 t2)
				(= t2 t3))
			(if 	("time interval1 is part of time interval2" t2 t1)
				(= t1 t3)))))

(forall ((t1 "time interval") (t2 "time interval"))
	(exists ((t3 "time interval"))
		(and 	("time interval1 plus time interval2 is time interval3" t1 t2 t3)
			("time interval1 is part of time interval2" t1 t3)
			("time interval1 is part of time interval2" t2 t3))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval") (t3 "time interval")) 
	(if 	(and 	("time interval1 is part of time interval2" t1 t3)
			("time interval1 is part of time interval2" t2 t3)
			("time interval1 plus time interval2 is time interval3" t1 t2 t4))
		("time interval1 is part of time interval2" t4 t3)))

(forall ((t1 "time interval") (t2 "time interval"))
	(exists ((t3 "time interval"))
		(and 	("time interval1 plus time interval2 is time interval3" t1 t2 t3)
			(forall ((t4 "time interval"))
				(if 	("time interval1 plus time interval2 is time interval3" t1 t2 t4)
					(= t4 t3))))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval"))
	(iff 	("time interval1 to time interval2 specifies time interval3" t1 t2 t3)
		(or 	(and 	("time interval1 is properly before time interval2" t1 t2)
				(exists ((t4 "time interval"))
					(and 	("time interval1 meets time interval2" t4 t2)
						("time interval1 meets time interval2" t1 t4)
						("time interval1 plus time interval2 is time interval3" t1 t4 t3)))
			(and 	("time interval1 meets time interval2" t1 t2)
				(= t1 t3))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(exists (t3 "time interval")
		(iff 	("time interval1 starts time interval2 complementing time interval3" t1 t2 t3)
			(if 	("time interval1 starts time interval2" t1 t2)
				(and 	("time interval1 finishes time interval2" t3 t2)
					("time interval1 meets time interval2" t1 t3))))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval"))
	(if 	("time interval1 starts time interval2 complementing time interval3" t1 t2 t3)
		(forall ((t4 "time interval"))
			(if 	(and 	("time interval1 is part of time interval2" t4 t2)
					(not ("time interval1 overlaps time interval2" t4 t1)))
				("time interval1 is part of time interval2" t4 t3)))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval") (t4 "time interval"))
	(if	(and	("time interval1 starts time interval2 complementing time interval3" t1 t2 t3)
			("time interval1 starts time interval2 complementing time interval3" t1 t2 t4))
	(= t3 t4)))

(forall ((t1 "time interval") (t2 "time interval"))
	(exists (t3 "time interval")
		(iff 	("time interval1 finishes time interval2 complementing time interval3" t1 t2 t3)
			(if 	("time interval1 finishes time interval2" t1 t2)
				(and 	("time interval1 starts time interval2" t3 t2)
					("time interval1 meets time interval2" t3 t1))))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval"))
	(if 	("time interval1 finishes time interval2 complementing time interval3" t1 t2 t3)
		(exists ((t4 "time interval"))
			(if 	(and 	("time interval1 is part of time interval2" t4 t2)
					(not ("time interval1 overlaps time interval2" t4 t1)))
				("time interval1 is part of time interval2" t4 t3)))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval") (t4 "time interval"))
	(if 	(and	("time interval1 finishes time interval2 complementing" t1 t2 t3)
			("time interval1 finishes time interval2 complementing" t1 t2 t4))
	(= t3 t4)))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval"))
	(iff 	("time interval1 intersects time interval2 with time interval3" t1 t2 t3)
		(if 	("time interval1 overlaps time interval2" t1 t2)
			(and 	("time interval1 finishes time interval2" t3 t1)
				("time interval1 starts time interval2" t3 t2)))))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval")(t4 "time interval"))
	(if 	(and 	("time interval1 intersects time interval2 with time interval3" t1 t2 t3)
			("time interval1 is part of time interval2" t4 t1)
			("time interval1 is part of time interval2" t4 t2))
		("time interval1 is part of time interval2" t4 t3)))

(forall ((t1 "time interval") (t2 "time interval") (t3 "time interval") (t4 "time interval"))
	(if 	(and	("time interval1 overlaps time interval2" t1 t2)
			("time interval1 intersects time interval2" t1 t2 t3)
			("time interval1 intersects time interval2" t1 t2 t4))
		(= t3 t4)))

(forall ((t1 "time interval") (t2 "time interval"))
	(if 	("time interval1 is properly before time interval2" t1 t2)
		(exists ((t3 "time interval") (t4 "time interval") (t5 "time interval") (t6 "time interval"))
			(and 	("time interval1 meets time interval2" t1 t3)
				("time interval1 meets time interval2" t3 t2)
				("time interval1 plus time interval2 is time interval3" t1 t2 t4)
				("time interval1 starts time interval2 complementing time interval3" t1 t4 t5)
				("time interval1 finishes time interval2 complementing time interval3" t2 t4 t6)
				("time interval1 intersects time interval2 with time interval3" t5 t6 t3)))))

(forall ((d1 duration))
	(leq d1 d1))

(forall ((d1 duration) (d2 duration))
	(or 	(leq d1 d2)
		(leq d2 d1)))

(forall ((d1 duration) (d2 duration))
	(if 	(and 	(leq d1 d2)
			(leq d2 d1))
		(= d1 d2)))

(forall ((d1 duration) (d2 duration) (d3 duration))
	(if 	(and 	(leq d1 d2)
			(leq d2 d3))
		(leq d1 d3)))

(forall ((d1 duration) (d2 duration))
	(iff 	(lt d1 d2)
		(and 	(leq d1 d2)
			(not (= d2 d1)))))

(forall ((d1 duration) (d2 duration) (d3 duration) (d4 duration))
	(= (+ (+ d1 d2) d3) (+ d1 (+ d2 d3))))

(forall ((d1 duration) (d2 duration))
	(= (+ d1 d2) (+ d2 d1)))

(forall ((d1 duration))
	(exists ((d2 duration))
		(= D0 (+ d1 d2))))

(forall ((n1 number) (d1 duration))
	(exists (d2 duration)
		(= (mult n1 d1) d2)))

(forall  ((d1 duration) (d2 duration) (n1 number))  
	(exists ((d3 duration))    
		(if 	(= d3 (mult n1 (+ d1 d2)))
			(= d3 (+ (mult n1 d1) (mult n1 d2))))))

(forall ((d1 duration) (n1 number) (n2 number))
	(= (mult (+ n1 n2) d1) (+ (mult n1 d1) (mult n2 d1))))

(forall ((d1 duration))
	(= (mult 0 d1) D0))

(forall ((n1 number) (d1 duration))          
	(exists ((d2 duration))
		(iff    (= D0 (* n1 d1))
			(or     (= n1 0)
                                (= d1 D0)))))

(forall ((d1 duration))
	(if 	(not (= d1 D0))
		(exists ((d2 duration) (n1 number))
			(= (mult d1 n1) d2))))

(forall ((t "time interval") (d1 duration) (d2 duration))
	(if 	(and 	("time interval has duration" t d1)
			("time interval has duration" t d2))
		(= d1 d2)))

(forall ((t "time interval"))
	(gt ("duration of" t) D0))

(forall ((t "time interval"))
	(not (= ("duration of" t) D0)))

(forall ((t1 "time interval") (t2 "time interval"))
	(not (= D0 (+ ("duration of" t1) ("duration of" t2)))))

(forall ((t1 "time interval") (t2 "time interval"))
	(if 	("time interval1 is part of time interval2" t1 t2)
		(leq ("duration of time interval" t1) ("duration of time interval" t2))))

(forall ((t1 "time interval") (t2 "time interval"))
	(if 	("time interval1 meets time interval2" t1 t2)
		(exists ((t3 "time interval") (d3 duration))
			(and 	("time interval3 equals time interval1 plus time interval2" t3 t1 t2)
				("duration3 equals duration1 plus duration2" d3 ("duration of" t1) ("duration of" t2))
				(= d3 ("duration of" t3))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(exists ((t3 "time interval"))
		(if 	("time interval1 starts time interval2 complementing time interval3" t1 t2 t3)
			(= ("duration of" t2) (+ ("duration of" t3) ("duration of" t1))))))

(forall ((t1 "time interval") (t2 "time interval"))
	(exists ((t3 "time interval"))
		(if 	("time interval1 finishes time interval2 complementing time interval3" t1 t2 t3)
			(= ("duration of" t2) (+ ("duration of" t3) ("duration of" t1))))))

(forall ((o occurrence) (t "time interval"))
	(iff 	("occurrence occurs within time interval" o t)
		(exists ((t2 "time interval"))
			(and 	("time interval1 is part of time interval2" t2 t)
				("occurrence occurs throughout time interval" o t2)))))

(forall ((o occurrence) (t "time interval"))
	(iff 	("occurrence occurs for time interval" o t)
		(and 	("occurrence occurs throughout time interval" o t)
			(exists ((t2 "time interval"))
				(and 	("time interval1 meets time interval2" t2 t)
					(not ("occurrence occurs within time interval" o t2))))
			(exists ((t3 "time interval"))
				(and 	("time interval1 meets time interval2" t t3)
					(not ("occurrence occurs within time interval" o t3)))))))

(forall ((o occurrence) (d duration))
	(iff 	("occurrence lasts for duration" o d)
		(exists ((t "time interval"))
			(and 	("occurrence occurs for time interval" o t)
				("time interval has duration" t d)))))

(forall ((o1 occurrence) (o2 occurrence))
	(iff 	("occurrence1 precedes occurrence2" o1 o2)
		(forall ((t1 "time interval") (t2 "time interval"))
			(if 	(and 	("situation model occurs for time interval" o1 t1)
					("situation model occurs for time interval" o2 t2))
			("time interval1 precedes time interval2" t1 t2)))))

(forall ((o1 occurrence) (o2 occurrence) (o3 occurrence))
	(if 	(and 	("occurrence1 precedes occurrence2" o1 o2)
			("occurrence1 precedes occurrence2" o2 o3))
		("occurrence1 precedes occurrence2" o1 o3)))

(forall ((o1 occurrence) (o2 occurrence))
	(iff 	("occurrence1 starts before occurrence2" o1 o2)
		(forall ((t1 "time interval") (t2 "time interval"))
			(if 	(and 	("situation model occurs for time interval" o1 t1)
					("situation model occurs for time interval" o2 t2))
				("time interval1 starts before time interval2" t1 t2)))))

(forall ((o1 occurrence) (o2 occurrence))
	(iff 	("occurrence1 ends before occurrence2" o1 o2)
		(forall ((t1 "time interval") (t2 "time interval"))
			(if 	(and 	("situation model occurs for time interval" o1 t1)
					("situation model occurs for time interval" o2 t2))
				("time interval1 ends before time interval2" t1 t2)))))

(forall ((s1 "situation model") (s2 "situation model"))
	(iff 	("situation model1 precedes situation model2" s1 s2)
		(forall ((o1 occurrence) (o2 occurrence))
			(if 	(and 	("situation model has occurrence" s1 o1)
					("situation model has occurrence" s2 o2))
				("occurrence1 precedes occurrence2" o1 o2)))))

(forall ((s1 "situation model") (s2 "situation model"))
	(iff 	("situation model1 starts before situation model2" s1 s2)
		(forall ((o1 occurrence) (o2 occurrence))
			(if 	(and 	("situation model has occurrence" s1 o1)
					("situation model has occurrence" s2 o2))
				("occurrence1 starts before occurrence2" o1 o2)))))

(forall ((s1 "situation model") (s2 "situation model"))
	(iff 	("situation model1 ends before situation model2" s1 s2)
		(forall ((o1 occurrence) (o2 occurrence))
			(if 	(and 	("situation model has occurrence" s1 o1)
					("situation model has occurrence" s2 o2))
				("occurrence1 ends before occurrence2" o1 o2)))))


(forall ((part thing)) 
	("part of" part part))

(forall ((part thing) (whole thing)) 
	(if 	(and 	("part of" part whole)
			("part of" whole part))
		(= part whole))) 

(forall ((part thing) (whole thing) (part3 thing))  
	(if 	(and 	("part of" part whole)
			("part of" whole part3))
		("part of" part part3)))

(forall ((part1 thing) (part2 thing)) 
	(iff 	(overlaps part1 part2)
		(exists ((part3 thing))
			(and	("part of" part3 part1)
				("part of" part3 part2)))))

(forall ((whole thing) (part thing)) 
	(iff 	("proper part" part whole)
		(and	("part of" part whole)
			(not ("part of" whole part)))))

(forall ((part1 thing) (whole thing)) 
	(if 	("proper part" part1 whole)
		(exists ((part2 thing))
			(and	("proper part" part2 whole)
				(not (overlaps part2 part1))))))

)
