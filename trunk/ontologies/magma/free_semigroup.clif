
(cl-text free_semigroup

(forall (x y z)
	(= (op x (op y z)) (op (op x y) z)))

(forall (x y z w)
	(if	(= (op x y) (op z w))
		(or	(and	(= x z)
				(= y w))
			(exists (u)
				(or	(and	(= z (op x u))
						(= y (op u w)))
					(and	(= x (op z u))
						(= w (op u y))))))))

(exists (x)
	(forall (y z)
		(not (= x (op y z)))))

(forall (x)
	(iff	(generator x)
		(not (exists (y z)
			(= x (op y z))))))

(forall (x y)
	(iff	(subword x y)
		(exists (u v)
			(= y (op u (op x v))))))

(forall (x y)
	(iff	(initial_subword x y)
		(exists (u)
			(= y (op x u)))))

(forall (x y)
	(iff	(end_subword x y)
		(exists (u)
			(= y (op u x)))))

(forall (x y z)
	(iff	(= z (append x y))
		(and	(generator x)
			(= z (op y x)))))

)
