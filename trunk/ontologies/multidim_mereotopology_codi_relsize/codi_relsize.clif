/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 3.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/3.0/legalcode.
 *
 * Contributors:
 *    Torsten Hahmann - initial implementation
 *******************************************************************************/

(cl-text multidim_mereotopology_codi_size/codi_relsize.clif

(cl-imports multidim_mereotopology_codi/codi.clif)


(cl-comment 'Axioms of Relative Size for entities of equal dimension')


(cl-comment 'Definition of EqSize in terms of LeqSize')

(forall (x y)
    (iff
        (EqSize x y)
        (and
            (LeqSize x y)
            (LeqSize y x)
        )
    )
)


(cl-comment 'LeqSize is antisymmetric')

(forall (x y)
    (if
        (and
            (LeqSize x y)
            (LeqSize y x)
        )
        (EqSize x y)
    )
)


(cl-comment 'EqSize is reflexive')

(forall (x)
    (EqSize x x)
)

(cl-comment 'Definition of LessSize in terms of LeqSize')

(forall (x y)
    (iff
        (LesserSize x y)
        (and
            (LeqSize x y)
            (not (LeqSize y x))
        )
    )
)

(cl-comment 'Cont requires a lesser or equal size')

(forall (x y)
    (if
        (Cont x y)
        (LeqSize x y)
     )
)

(cl-comment 'Proper containment requires a lesser or equal size')

(forall (x y)
    (if
        (and
            (Cont x y)
            (not (= x y))
        )
        (LesserSize x y)
     )
)

(cl-comment 'Any two objects of equal dimension can be compared in size')

(forall (x y)
    (if
        (EqDim x y)
        (or
            (LeqSize x y)
            (LeqSize x y)
        )
    )
)


(cl-comment 'Nothing is larger than infinite size')

(forall (x y)
    (if
        (InfiniteSize x)
        (LeqSize y x)
    )
)


(cl-comment 'Two equidimensional things of infinite size are of equal size')

(forall (x y)
    (if
        (and
            (InfiniteSize x)
            (InfiniteSize y)
            (EqDim x y)
        )
        (EqSize x y)
    )
)


(cl-comment 'An infinite sized object of greater dimension is greater than an infinite sized object of smaller dimension: establishes a size order for infinite sized object based purely on their dimension')

(forall (x y)
    (if
        (and
            (InfiniteSize x)
            (InfiniteSize y)
            (< x y)
        )
        (LesserSize x y)
    )
)




)

