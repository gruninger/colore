
(cl-text http://colore.oor.net/interval_meeting/im_def.clif

(cl-imports http://colore.oor.net/interval_meeting/meets_root.clif)

(forall (n)
(iff	(moment n)
	(exists (i m)
		(and	(meets i n)
			(meets n m)
			(not (exists (j k)
				(and 	(meets i j)
					(meets j k)
					(meets k m))))))))

(forall (i j)
(iff	(precedes i j)
	(exists (k)
		(and	(meets i k)
			(meets k j)))))

(forall (i j)
(iff	(overlaps i j)
	(exists (k m n o p)
		(and	(meets k m)
			(meets m n)
			(meets n o)
			(meets o p)
			(meets m j)
			(meets j p)
			(meets k i)
			(meets i o)))))

(forall (i j)
(iff	(starts i j)
	(exists (k m n)
		(and	(meets k i)
			(meets i m)
			(meets m n)
			(meets k j)
			(meets j n)))))

(forall (i j)
(iff	(during i j)
	(exists (k m n o)
		(and	(meets k m)
			(meets m i)
			(meets i n)
			(meets n o)
			(meets k j)
			(meets j o)))))

(forall (i j)
(iff	(finishes i j)
	(exists (k m n)
		(and	(meets k m)
			(meets m i)
			(meets i n)
			(meets k j)
			(meets j n)))))

(forall (p q r s)
	(iff	(equiv p q r s)
		(and	(meets p q)
			(meets r s)
			(meets p s))))

(forall (p q r s)
	(iff	(pointless p q r s)
		(exists (u v w)
			(and	(equiv p q u v)
				(equiv r s v w)))))
)
