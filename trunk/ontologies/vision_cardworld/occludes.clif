/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 3.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/3.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
 *******************************************************************************/

(cl-text occludes

(cl-imports obscures)

(cl-imports interior_depicts)

(forall (p q s t) 
	(if 	(and 	(point p) 
			(part p s) 
			(position p t) 
			(depict q p)) 
		(not (exists (s1) 
			(obscures s1 s t)))))

(forall (l e s) 
	(if 	(and 	(depict l e) 
			(part e s)) 
		(exists (p t) 
			(and 	(part p e) 
				(position p t) 
				(not (exists (s1) 
					(obscures s1 s t)))))))

(forall (s r) 
	(if 	(surface s) 
		(iff 	(depict r s) 
			(exists (t) 
				(and 	(interior s t) 
					(inclusion r t) 
					(not (exists (s1) 
						(obscures s1 s t))))))))

(forall (e s l) 
	(if 	(and 	(edge e) 
			(part e s) 
			(depict l e)) 
		(or 	(exists (p) 
				(and 	(point p) 
					(part p e) 
					(lacunate p))) 
			(exists (l1 l2 j1 j2 e1 e2) 
				(and 	(depict l1 e1) 
					(depict l2 e2) 
					(intersect l l1 j1) 
					(intersect l l2 j2) 
					(or 	(part e1 s) 
						(occluding e1 s) 
						(exists (t) 
							(abuts e e1 t))) 
					(or 	(part e2 s) 
						(occluding e2 s)
						(exists (t) 
							(abuts e e2 t))))))))

)
