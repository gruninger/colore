/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 3.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/3.0/legalcode.
 *
 * Contributors:
 *    Torsten Hahmann - initial implementation
 *******************************************************************************/

(cl-text http://colore.oor.net/multidim_space_codib/codib_down.clif

(cl-imports http://colore.oor.net/multidim_space_codi/codi_down.clif)

(cl-imports http://colore.oor.net/multidim_space_codib/definitions/closed.clif)

(cl-imports http://colore.oor.net/multidim_space_codib/definitions/icont.clif)

(cl-imports http://colore.oor.net/multidim_space_codib/definitions/tcont.clif)

(cl-comment 'ME-E1: atomic')

(forall (x)
	(if
		(and
			(S x)
			(not (ZEX x))
		)
		(exists (y)
			(and
				(P y x)
				(Min y)
			)
		)
	)
)

(cl-comment 'BC-A1: entities in the boundary are at least of a dimension lower than the bounded entity')

(forall (x y)
	(if
		(and
			(S x)
			(S y)
			(BCont x y)
		)
		(and
			(Cont x y)
			(Inc x y)
		)
	)
) 

(cl-comment 'BC-A2: if z is contained in two superficially connected entities x and y embedded in an entity v of the dimension of x, that is x has locally a codimension of zero, then z is contained in the boundary of x')

(forall (x y v z)
	(if
		(and
			(S x)
			(S y)
			(S v)
			(S z)
			(SC x y)
			(Min x)
			(P x v)
			(Cont y v)
			(Cont z x)
			(Cont z y)
		)
		(BCont z x)
	)
) 



(cl-comment 'BC-A3: any z contained in two superficially connected parts x and y of v that is exactly of one dimension lower than v is not in the boundary of v')

(forall (x y z v)
	(if
		(and
			(S x)
			(S y)
			(S v)
			(S z)			
			(SC x y)
			(P x v)
			(P y v)
			(Cont z x)
			(Cont z y)
			(Covers v z)
		)
		(not (BCont z v))
	)
) 


(cl-comment 'BC-A4')

(forall (x y z)
	(if
		(and
			(S x)
			(S y)
			(S z)
			(BCont x y)
			(P y z)
			(forall (v w)
				(if
					(and
						(S v)
						(S w)
						(P v z)
						(not (PO v y))
						(P w x)
					)
					(not (Cont w v))
				)
			)
		)
		(BCont x z)
	)
)


(cl-comment 'BC-A5: transitivity with respect to Cont')

(forall (x y z)
	(if
		(and
			(S x)
			(S y)
			(S z)
			(BCont x y)
			(Cont z x)
		)
		(BCont z y)
	)
)
(cl-comment 'BC-A6')

(forall (x y)
	(iff
		(and
			(S x)
			(S y)
			(BCont x y)
		)
		(and
			(S x)
			(S y)
			(not (ZEX x))
			(forall (z)
				(if
					(and
						(P z x)
						(Min z)
					)
					(BCont z y)
				)
			)
		)
	)
)
				
)