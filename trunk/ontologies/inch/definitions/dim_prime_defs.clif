/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 3.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/3.0/legalcode.
 *
 * Contributors:
 *    Torsten Hahmann - initial implementation
 *******************************************************************************/

(cl-text http://colore.oor.net/inch/definitions/dim_prime_defs.clif

(cl-comment "compact axioms for linear relative dimension")

(cl-comment "D-D2: define EqDim in terms of <")

(forall (x y)
	(iff 
		(EqDim x y)
		(and 	(not (< x y))
				(not (< y x))
		)
	)
)


(cl-comment "D-D3: <=")

(forall (x y)
	(iff (<= x y) (or (< x y) (EqDim x y)))
)


(cl-comment "D-D4: Maximal dimension (Codim=0)")
(forall (x)
	(iff	(MaxDim x)
			(forall (y) 
					(<= y x)
			)
	)
)


(cl-comment "D-D5: Minimal dimension (Dim=0)")
(forall (x)
	(iff	(MinDim x)
		(and
			(not (ZEX x))
			(forall (y)
				(if
					(< y x)
					(ZEX y)
				)
			)	
		)
	)
)


)