(cl-text http://colore.oor.net/between/veblen.clif

(cl-imports http://colore.oor.net/between/infinite_bet.clif)

(cl-comment "(cl-imports http://colore.oor.net/between/pasch.clif)")

(exists (p1 p2)
	(not	(= p1 p2)))
	
(forall (a b c)
	(if	(between a b c)
		(between c b a)))
		
(forall (a b c)
	(if	(between a b c)
		(not	(between b c a))))
		
(forall (a b c)
	(if	(between a b c)
		(not (= a c))))
		
(forall (a b)
	(if	(not (= a b))
		(exists (c)
			(between a b c))))
			
(forall (a b c)
	(iff	(collinear a b c)
		(and	(not (= a b))
			(not (= a c))
			(not (= b c))
			(or	(between a b c)
				(between b c a)
				(between c a b)))))
						
(forall (a b c d)
	(if	(and	(collinear a b c)
			(collinear a b d)
			(not (= c d)))
		(collinear a c d)))
		
(exists (a b c)
	(and	(not (= a b))
		(not (= a c))
		(not (= b c))
		(not	(or	(between a b c)
				(between b c a)
				(between c a b)))))
						
(forall (a b c d e)
	(if	(and	(not (collinear a b c))
			(between b c d)
			(between c e a)
			(not (= a b))
			(not (= a c))
			(not (= b c)))
		(exists (f)
			(and	(between a f b)
				(collinear d e f)))))
					
)


