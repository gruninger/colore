/*******************************************************************************
 * Copyright (c) University of Toronto and others. All rights reserved. 
 * The content of this file is licensed under the Creative Commons Attribution-
 * ShareAlike 3.0 Unported license. The legal text of this license can be
 * found at http://creativecommons.org/licenses/by-sa/3.0/legalcode.
 *
 * Contributors:
 *    Michael Gruninger - initial implementation
 *******************************************************************************/

(cl-module interval-meeting.clif

(forall (i j)
(if	(meets i j)
	(and	(timeinterval i)
		(timeinterval j))))

(forall (i j k m)
(if	(and	(meets i k)
		(meets j k)
		(meets i m))
	(meets j m)))

(forall (i)
	(exists (j k)
		(and	(meets j i)
			(meets i k))))

(forall (i j k m)
(if	(and	(meets i j)
		(meets k l))
	(or	(meets i l)
		(exists (n)
			(or	(and	(meets i n)
					(meets n l))
				(and	(meets k n)
					(meets n j)))))))

(forall (i j)
(if	(meets i j)
	(not (meets j i))))

(forall (i j k m)
(iff	(and	(meets i j)
		(meets j k)
		(meets k m))
	(and	(meets i (plus j k))
		(meets (plus j k) m))))

(forall (i)
(iff	(moment i)
	(forall (j k)
		(not (= i (plus j k))))))

(forall (i j)
(iff	(precedes i j)
	(exists (k)
		(and	(meets i k)
			(meets k j)))))

(forall (i j)
(iff	(overlaps i j)
	(exists (k m n o p)
		(and	(meets k m)
			(meets m n)
			(meets n o)
			(meets o p)
			(meets m j)
			(meets j p)
			(meets k i)
			(meets i o)))))

(forall (i j)
(iff	(starts i j)
	(exists (k m n)
		(and	(meets k i)
			(meets i m)
			(meets m n)
			(meets k j)
			(meets j n)))))

(forall (i j)
(iff	(during i j)
	(exists (k m n o)
		(and	(meets k m)
			(meets m i)
			(meets i n)
			(meets n o)
			(meets k j)
			(meets j o)))))

(forall (i j)
(iff	(finishes i j)
	(exists (k m n)
		(and	(meets k m)
			(meets m i)
			(meets i n)
			(meets k j)
			(meets j n)))))

)
